pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://s01.oss.sonatype.org/content/repositories/devkeijitlv-1014" }
    }
}
rootProject.name = "TlvKsp"

def androidHome = System.getenv("ANDROID_HOME")
def localProperties = new File(settingsDir, "local.properties")
def hasSdkDir = false
if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
        properties.load(instr)
    }
    hasSdkDir = properties.getProperty("sdk.dir") != null
}

if (androidHome != null || hasSdkDir) {
    include ':app'
} else {
    println "Android SDK not found. Skipping ':app' module."
}

include ':tlv'
include ':tlv-ksp'
