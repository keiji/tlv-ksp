plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'jacoco'
    id 'signing'
    id "com.gradleup.nmcp"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

jacocoTestReport {
    reports {
        csv.required = true
        xml.required = true
        html.required = true
    }
}

group = rootProject.ext.groupId
version = rootProject.ext.versionName

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = 'tlv'

            from components.java

            pom {
                name = 'TLV'
                description = 'A lightweight Kotlin library for Tag-Length-Value (TLV) data handling.'
                url = 'https://github.com/keiji/tlv-ksp'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'keiji'
                        name = 'ARIYAMA Keiji'
                        email = 'keiji.ariyama@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/keiji/tlv-ksp.git'
                    developerConnection = 'scm:git:git@github.com:keiji/tlv-ksp.git'
                    url = 'https://github.com/keiji/tlv-ksp'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
