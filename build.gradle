import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.13.2' apply false
    id 'com.android.library' version '8.13.2' apply false
    id 'org.jetbrains.kotlin.android' version '2.3.0' apply false
    id 'org.jetbrains.kotlin.jvm' version '2.3.0' apply false
    id("io.gitlab.arturbosch.detekt") version "1.23.8"

    id("com.gradleup.nmcp") version "1.4.3" apply false
    id("com.gradleup.nmcp.aggregation") version "1.4.3"
}

ext {
    groupId = 'dev.keiji.tlv'
    versionName = '0.3.4'
}

tasks.register('reportMerge', ReportMergeTask) {
    output = layout.buildDirectory.file("reports/detekt/detekt.xml")
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        buildUponDefaultConfig = true
        allRules = true
        config.setFrom("$rootDir/config/detekt/detekt.yml")
        parallel = true
    }

    afterEvaluate { project ->
        project.tasks.withType(Detekt) {
            finalizedBy reportMerge
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

nmcpAggregation {
    centralPortal {
        username = System.getenv("CENTRAL_PORTAL_USERNAME")
        password = System.getenv("CENTRAL_PORTAL_PASSWORD")
        publishingType = "USER_MANAGED"
    }
}

dependencies {
    nmcpAggregation(project(":tlv"))
    nmcpAggregation(project(":tlv-ksp"))
}
