plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

group = rootProject.ext.groupId
version = rootProject.ext.versionCode

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${group}"
            artifactId = 'tlv'
            version = "${version}"

            from components.java

            pom {
                name = 'TLV'
                description = 'TLV is a Kotlin library for encoding and decoding TLV data.'
                url = 'https://github.com/keiji/tlv-ksp'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'keiji'
                        name = 'ARIYAMA Keiji'
                        email = 'keiji.ariyama@gmail.com'
                    }
                }
                scm {
                    connection = 'https://github.com/keiji/tlv-ksp.git'
                    developerConnection = 'https://github.com/keiji/tlv-ksp.git'
                    url = 'https://github.com/keiji/tlv-ksp'
                }
            }
        }
    }

    Properties sonaTypeProps = new Properties()
    File sonatypePropertiesFile = project.rootProject.file("sonatype.properties")
    if (sonatypePropertiesFile.exists()) {
        sonaTypeProps.load(sonatypePropertiesFile.newDataInputStream())
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('-SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            def sonatypeUsername = sonaTypeProps.get('sonatypeUsername') ?: ""
            def sonatypePassword = sonaTypeProps.get('sonatypePassword') ?: ""
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
